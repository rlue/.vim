" https://github.com/junegunn/fzf.vim ------------------------------------------
let g:fzf_plugin_path =
      \ get(
      \   filter(
      \     [fnamemodify(resolve(exepath('fzf')), ':h:h'),
      \      fnamemodify(resolve(exepath('fzf')), ':h:h') . '/share/doc/fzf/examples'],
      \     '!empty(globpath(v:val, "/plugin/fzf.vim"))'
      \   ), 0
      \ )

if v:version >= 740 && !empty(g:fzf_plugin_path)
      \ && (!has('gui_running') || has('terminal'))
  Plug g:fzf_plugin_path
  Plug 'junegunn/fzf.vim'

  nnoremap <Leader>zf :Files<CR>
  nnoremap <Leader>zb :Buffers<CR>
  nnoremap <Leader>zl :Lines<CR>
  nnoremap <Leader>zh :Helptags<CR>

  if executable('rg') && (exists(':Rg') != 2)
    command! -bang -nargs=* Rg
      \ call fzf#vim#grep(
      \   'rg --column --line-number --no-heading --color=always '.shellescape(<q-args>), 1,
      \   <bang>0 ? fzf#vim#with_preview('up:60%')
      \           : fzf#vim#with_preview('right:50%', '?'),
      \   <bang>0)
    nnoremap <Leader>zg :Rg<CR>
  endif
endif

" vi:ft=vim
